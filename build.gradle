import org.apache.tools.ant.filters.ReplaceTokens
apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'eclipse'

jar.archiveName = project.name + '.jar'

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
	mavenCentral()

	// Spigot
	maven {
		url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
	}

	// Bungeecord
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}

	// HolographicDisplays
	maven {
		url 'https://repo.codemc.io/repository/maven-public/'
	}

	// Citizens
	maven {
		url "http://repo.citizensnpcs.co/"
	}

	// Jitpack
	maven {
		url 'https://jitpack.io'
	}

	maven {
		url 'https://maven.pkg.github.com/Sitrica/SourCore/'
		credentials {
            username = "Sitrica"
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_PACKAGES_KEY")
        }
	}

}

dependencies {

	// HolographicDisplays
	implementation (group: 'com.gmail.filoghost.holographicdisplays', name: 'holographicdisplays-plugin', version: '2.4.9') {
		exclude group: 'org.bstats', module: 'bstats-bukkit-lite'
	}

	// Citizens
	implementation (group: 'net.citizensnpcs', name: 'citizensapi', version: '2.0.27-SNAPSHOT')

	// Spigot
	implementation (group: 'org.spigotmc', name: 'spigot-api', version: '1.16.1-R0.1-SNAPSHOT')

	// Vault
	implementation (group: 'com.github.MilkBowl', name: 'VaultAPI', version: '1.7')

	// AnvilGUI
	shade (group: 'com.github.WesJD', name: 'AnvilGUI', version: 'master-SNAPSHOT')

	// Smart-Invs
	shade (group: 'fr.minuskube.inv', name: 'smart-invs', version: '1.2.7')

	// H2 Database
	shade (group: 'com.h2database', name: 'h2', version: '1.4.200')

	// Sitrica Core
	shade (group: 'com.sitrica', name: 'core', version: '1.0.10')

	shade fileTree(dir: 'libraries', include: '*.jar')

}

configurations {
	runtime.exclude group: 'org.bstats', module: 'bstats-bukkit-lite'
}

publishing {
	repositories {
		maven {
			name = project.name
			url = uri("https://maven.pkg.github.com/Sitrica/" + project.name)
			credentials {
				username = 'Sitrica'
				password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_PACKAGES_KEY")
			}
		}
	}
	publications {
		gpr(MavenPublication) {
			version = project.property("version")
			groupId = 'com.sitrica'
			artifactId = project.name
			from(components.java)
		}
	}
}

processResources {
	from (sourceSets.main.resources.srcDirs) {
		filter ReplaceTokens, tokens: ["version": project.property("version")]
		include '**/*.yml'
	}
}

jar {
	configurations.shade.each { dep ->
		from (project.zipTree(dep)) {
			exclude '**/*.php'
			exclude '**/*.yml'
			exclude 'META-INF', 'META-INF/**'
		}
	}
}
